# Maintainer: Adam Hellberg <sharparam@sharparam.com>

pkgname=facti-git
_base="${pkgname%-git}"
pkgver=v0.1.0.r31.g229025f
pkgrel=1
pkgdesc="CLI tool that aids in Factorio mod development"
arch=(x86_64 i686 aarch64)
url="https://facti.rs"
license=(MPL2)
makedepends=(git cargo)
provides=("${_base}")
conflicts=("${_base}")
options=(!lto)
source=("${_base}::git+https://github.com/Sharparam/facti.git")
sha512sums=(SKIP)
_binname="${_base}"
_pkgid="${_base}"

pkgver() {
  cd "${_base}"
  ./bin/version --long cli | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${_base}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${_base}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --workspace
  cargo xtask man
  mkdir -p target/assets/completion
  cargo run --frozen --release --package facti -- completion bash > target/assets/completion/bash
  cargo run --frozen --release --package facti -- completion zsh > target/assets/completion/zsh
  cargo run --frozen --release --package facti -- completion fish > target/assets/completion/fish
}

check() {
  cd "${_base}"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --workspace
}

package() {
  cd "${_base}"
  install -Dm755 "target/release/${_binname}" "${pkgdir}/usr/bin/${_binname}"

  install -Dm644 -t "${pkgdir}/usr/share/man/man1" target/assets/man/*
  install -Dm644 target/assets/completion/bash "${pkgdir}/usr/share/bash-completion/completions/${_binname}"
  install -Dm644 target/assets/completion/zsh "${pkgdir}/usr/share/zsh/site-functions/_${_binname}"
  install -Dm644 target/assets/completion/fish "${pkgdir}/usr/share/fish/vendor_completions.d/${_binname}.fish"

  install -Dm644 crates/cli/README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
